
import org.apache.tools.ant.filters.*

apply plugin: 'java-library'

dependencies {
  implementation project(':core')
  testImplementation project(':lang:polish')
  testImplementation project(':lang:lucene')
  testImplementation deps.lucene.analysis.icu
  testImplementation deps.junit
}

configurations {
  distribution
}

task buildDistribution(type: Sync) {
  dependsOn configurations.testRuntimeClasspath

  def antPrj = new org.apache.tools.ant.Project()
  afterEvaluate {
    replaceTokens.each { k, v -> antPrj.setProperty(k.toString(), v.toString()) }
  }

  inputs.properties replaceTokensTrigger

  doFirst {
    antPrj.setProperty("version.lucene-analysis-icu", getResolvedVersion(
            configurations.testRuntimeClasspath,
            deps.lucene.analysis.icu.get()
    ))
    antPrj.setProperty("version.junit", getResolvedVersion(
            configurations.testRuntimeClasspath,
            deps.junit.get()
    ))
  }

  from(project.projectDir, {
    include 'src/**'
    exclude 'src/main/assembly/**'
    includeEmptyDirs false
    filteringCharset = 'UTF-8'
    filter(LineContains, negate: true, contains: ['fragment-start{'])
    filter(LineContains, negate: true, contains: ['fragment-end{'])
  })

  from(files("src/main/assembly"), {
    filteringCharset = 'UTF-8'
    filter(ExpandProperties, project: antPrj)
  })

  from(rootProject.projectDir, {
    include "gradlew*", "gradle/wrapper/**"
  })

  into "$buildDir/distribution"
}

artifacts {
  distribution file("$buildDir/distribution"), {
    builtBy tasks.buildDistribution
  }
}
